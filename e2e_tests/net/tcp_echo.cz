// Single-threaded TCP over loopback: on Linux a connect to a listening
// socket completes in the kernel backlog before accept runs, so one thread
// can drive both ends deterministically. Port 0 requests an ephemeral port,
// read back through local_addr, so parallel test runs never collide.
import net.{ Tcp, TcpListener }

let listener = TcpListener.bind("127.0.0.1", 0)!
let port = int64.parse(listener.local_addr()!.split(":")[1])!

let client = Tcp.connect("127.0.0.1", port)!
let server = listener.accept()!

client.write(to_bytes("hello server"))!
println(to_text(server.read(64)!)!)

server.write(to_bytes("hello client"))!
println(to_text(client.read(64)!)!)

// The accepted socket's peer is the client's local address.
println(server.peer_addr()! == client.local_addr()!)

// A read with nothing pending fails once the timeout elapses instead of
// blocking forever. The OS error text varies, so only the branch is printed.
client.set_timeout(50)!
match client.read(1) {
    Ok { value } => println("unexpected data"),
    Err { error } => println("timed out"),
}

client.close()!
server.close()!
listener.close()!
